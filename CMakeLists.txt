cmake_minimum_required(VERSION 3.9)

if(NOT CMAKE_TOOLCHAIN_FILE)
  message(FATAL_ERROR "You must specify CMAKE_TOOLCHAIN_FILE")
endif()

set(CMAKE_DISABLE_SOURCE_CHANGES ON)
set(CMAKE_DISABLE_IN_SOURCE_BUILD ON)

project(saya VERSION 0.1.0 LANGUAGES C CXX)

# get_directory_property(SAYA_BUILD_ACTIVE_SOURCE_TREE PARENT_DIRECTORY)
# if(SAYA_BUILD_ACTIVE_SOURCE_TREE)
  # message(FATAL_ERROR "You should find_package() instead of add_subdirectory()")
# endif()

set(
  CMAKE_MODULE_PATH
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake"
  ${CMAKE_MODULE_PATH}
)

if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE Release)
endif()
message(STATUS "saya configuration: ${CMAKE_BUILD_TYPE}")

# ------------------------------------------------

# Global build config

set(CMAKE_CXX_FLAGS "-std=c++14 -Wall -Wextra -pedantic-errors")
set(CMAKE_CXX_FLAGS_DEBUG "-g -O0") # -fno-inline -pg
set(CMAKE_CXX_FLAGS_RELEASE "-O2 -DNDEBUG")

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/lib")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/lib")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${PROJECT_SOURCE_DIR}/bin")

# ------------------------------------------------

find_package(Threads REQUIRED)

find_package(PkgConfig REQUIRED)
pkg_check_modules(ICU REQUIRED icu-uc)

find_package(OpenSSL REQUIRED)

# ---------------------------------------------

set(SAYA_DEFAULT_BOOST_VERSION "1.65.0")
message(STATUS "Boost sanity check...")

if(NOT BOOST_ROOT)
  message(FATAL_ERROR "You must set BOOST_ROOT")
endif()

message(STATUS "This branch of saya is last tested on Boost ${SAYA_DEFAULT_BOOST_VERSION}")

if(SAYA_FORCE_BOOST_VERSION)
  set(SAYA_BOOST_VERSION ${SAYA_FORCE_BOOST_VERSION})
else()
  set(SAYA_BOOST_VERSION ${SAYA_DEFAULT_BOOST_VERSION})
endif()

message(STATUS "Auto-setting Boost detection flags")
set(Boost_NO_SYSTEM_PATHS ON)
set(Boost_USE_MULTITHREADED ON)
set(Boost_NO_BOOST_CMAKE ON)
set(Boost_ADDITIONAL_VERSIONS "${SAYA_BOOST_VERSION}")

find_package(
  Boost ${SAYA_BOOST_VERSION} REQUIRED
  COMPONENTS
  program_options
  date_time
  filesystem
  system
  regex
)
message(STATUS "***Friendly notice*** You can safely ignore warnings generated by _Boost_COMPONENT_DEPENDENCIES")

if(NOT SAYA_BOOST_VERSION)
  message(FATAL_ERROR "Unknown error - could not determine Boost version")
endif()

message(STATUS "Using Boost ${SAYA_BOOST_VERSION}")
message(STATUS "Boost configuration is OK")

# Begin build ---------------------------------------
set(CMAKE_DEBUG_POSTFIX d)

# Exports
add_library(saya INTERFACE)
set(SAYA_INCLUDE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/include")
file(GLOB_RECURSE SAYA_HEADERS "${SAYA_INCLUDE_DIR}/saya" "*.hpp" "*.inc")

target_include_directories(saya INTERFACE "${SAYA_INCLUDE_DIR}")
target_sources(saya INTERFACE ${SAYA_HEADERS})
target_link_libraries(
  saya INTERFACE
  ${Boost_LIBRARIES} ${ICU_LIBRARIES} ${OPENSSL_LIBRARIES}
  ${CMAKE_THREAD_LIBS_INIT}
)
install(TARGETS saya EXPORT saya-export)

# non-header-only libraries
add_subdirectory(src)

# Tests
include(CTest)
add_subdirectory(test)

# Final export
export(
  EXPORT saya-export
  FILE saya-config.cmake
)

